# qmake configuration for linux-openwrt-g++ (MIPSEL / mipsel_24kc)

MAKEFILE_GENERATOR      = UNIX
CONFIG                 += cross_compile release
QMAKE_PLATFORM          = linux
include(../common/linux.conf) 
include(../common/gcc-base-unix.conf) 
include(../common/g++-unix.conf)

# ---- Toolchain (HARD-CODED; qmake won't expand $(TARGET_CROSS)) ----
QMAKE_CC                = mipsel-openwrt-linux-musl-gcc
QMAKE_CXX               = mipsel-openwrt-linux-musl-g++
QMAKE_LINK              = mipsel-openwrt-linux-musl-g++
QMAKE_LINK_SHLIB        = mipsel-openwrt-linux-musl-g++

QMAKE_AR                = mipsel-openwrt-linux-musl-gcc-ar cqs
QMAKE_NM                = mipsel-openwrt-linux-musl-gcc-nm -P
QMAKE_RANLIB            = mipsel-openwrt-linux-musl-gcc-ranlib
QMAKE_OBJCOPY           = mipsel-openwrt-linux-musl-objcopy
QMAKE_STRIP             = mipsel-openwrt-linux-musl-strip

# ---- Paths (these two placeholders will be replaced by Makefile using sed) ----
QMAKE_CFLAGS           += --sysroot=__TOOLCHAIN_DIR__ -I__STAGING_DIR__/usr/include
QMAKE_CXXFLAGS         += --sysroot=__TOOLCHAIN_DIR__ -I__STAGING_DIR__/usr/include -fno-exceptions
QMAKE_LFLAGS           += --sysroot=__TOOLCHAIN_DIR__ -L__STAGING_DIR__/usr/lib -latomic -Wl,-z,now -Wl,-z,relro

QMAKE_LIBS             += -lpthread
QMAKE_CFLAGS_RELEASE   += -O2
QMAKE_CXXFLAGS_RELEASE += -O2

load(qt_config)
